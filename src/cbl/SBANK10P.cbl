000100*****************************************************************
000200*                                                               *
000300*   Copyright (C) 1998-2010 Micro Focus. All Rights Reserved.   *
000400*   This demonstration program is provided for use by users     *
000500*   of Micro Focus products and may be used, modified and       *
000600*   distributed as part of your application provided that       *
000700*   you properly acknowledge the copyright of Micro Focus       *
000800*   in this material.                                           *
000900*                                                               *
001000*****************************************************************
001100
001200*****************************************************************
001300* Program:     SBANK10P.CBL (CICS Version)                      *
001400* Layer:       Screen handling                                  *
001500* Function:    Signon to system to identify user                *
001600*****************************************************************
001700
001800 IDENTIFICATION DIVISION.
001900 PROGRAM-ID.
002000     SBANK10P.
002100 DATE-WRITTEN.
002200     September 2002.
002300 DATE-COMPILED.
002400     Today.
002500
002600 ENVIRONMENT DIVISION.
002700
002800 DATA DIVISION.
002900 WORKING-STORAGE SECTION.
003000 01  WS-MISC-STORAGE.
003100   05  WS-PROGRAM-ID                         PIC X(8)
003200       VALUE 'SBANK10P'.
003300   05  WS-TRAN-ID                            PIC X(4).
003400   05  WS-BUSINESS-LOGIC-PGM                 PIC X(8)
003500       VALUE SPACES.
003600   05  WS-DYNAMIC-PGM                        PIC X(8)
003700       VALUE 'UNKNOWN'.
003800   05  WS-SAVED-EIBCALEN                     PIC S9(4) COMP.
003900
004000 01  MAPAREA                                 PIC X(2048).
004100 COPY MBANK10.
004200
004300 01  WS-TIME-DATE-WORK-AREA.
004400 COPY CDATED.
004500
004600 01  WS-BANK-DATA-AREAS.
004700   05  WS-BANK-DATA.
004800 COPY CBANKDAT.
004900   05  WS-BANK-EXT-DATA.
005000 COPY CBANKEXT.
005100
005200 COPY CSCRNHDD.
005300
005400 COPY CVERSND.
005500
005600 COPY DFHAID.
005700
005800 COPY DFHBMSCA.
005900
006000 COPY CABENDD.
006100
006200 LINKAGE SECTION.
006300 01  DFHCOMMAREA.
006400   05  FILLER                                PIC X(1)
006500       OCCURS 1 TO 32767 TIMES DEPENDING ON EIBCALEN.
006600
006700 PROCEDURE DIVISION.
006800*****************************************************************
006900* Write entry to log to show we have been invoked               *
007000*****************************************************************
007100*     COPY CTRACE.
000100*****************************************************************
000200*                                                               *
000300*  Copyright(C) 1998-2010 Micro Focus. All Rights Reserved.     *
000400*                                                               *
000500*****************************************************************
000600
000700*****************************************************************
000800* CTRACE.CPY                                                    *
000900*---------------------------------------------------------------*
001000* This copybook is used to provide an a trace of what           *
001100* transactions have been run so we get an idea of activity      *
001200* There are different versions for CICS and IMS.                *
001300*****************************************************************
001400*
001500* Comment out the instructions and recompile to not use the trace
001600     EXEC CICS LINK PROGRAM('STRAC00P')
001700                    COMMAREA(WS-PROGRAM-ID)
001800                    LENGTH(LENGTH OF WS-PROGRAM-ID)
001900    END-EXEC.
002000
002100* $ Version 5.99c sequenced on Wednesday 3 Mar 2011 at 1:00pm
007200
007300*****************************************************************
007400* Store our transaction-id                                      *
007500*****************************************************************
007600     MOVE EIBTRNID TO WS-TRAN-ID.
007700
007800*****************************************************************
007900* Store passed data or abend if there wasn't any                *
008000*****************************************************************
008100     IF EIBCALEN IS EQUAL TO 0
008200        MOVE WS-PROGRAM-ID TO ABEND-CULPRIT
008300        MOVE '0001' TO ABEND-CODE
008400        MOVE SPACES TO ABEND-REASON
008500*        COPY CABENDPO.
               PERFORM ZZ-ABEND
003000         GOBACK
003100
008600     ELSE
008700        MOVE EIBCALEN TO WS-SAVED-EIBCALEN
008800        MOVE LOW-VALUES TO WS-BANK-DATA
008900        MOVE DFHCOMMAREA (1:EIBCALEN)
009000          TO WS-BANK-DATA-AREAS (1:LENGTH OF WS-BANK-DATA-AREAS)
009100     END-IF.
009200
009300*****************************************************************
009400* This is the main process                                      *
009500*****************************************************************
009600
009700*****************************************************************
009800* Determine what we have to do (read from or send to screen)    *
009900*****************************************************************
010000     MOVE LOW-VALUE TO MAPAREA.
010100     EVALUATE TRUE
010200       WHEN BANK-MAP-FUNCTION-GET
010300         PERFORM SCREEN10-READ THRU
010400                 SCREEN10-READ-EXIT
010500       WHEN BANK-MAP-FUNCTION-PUT
010600         PERFORM SCREEN10-BUILD-AND-SEND THRU
010700                 SCREEN10-BUILD-AND-SEND-EXIT
010800       WHEN OTHER
010900         MOVE WS-PROGRAM-ID TO ABEND-CULPRIT
011000         MOVE '0002' TO ABEND-CODE
011100         MOVE SPACES TO ABEND-REASON
011200*         COPY CABENDPO.
               PERFORM ZZ-ABEND
011300     END-EVALUATE.
011400
011500* Call the appropriate routine to handle the business logic
011600     IF BANK-MAP-FUNCTION-GET
011700        EXEC CICS LINK PROGRAM(WS-BUSINESS-LOGIC-PGM)
011800                       COMMAREA(WS-BANK-DATA)
011900                       LENGTH(LENGTH OF WS-BANK-DATA)
012000        END-EXEC
012100     END-IF.
012200
012300*****************************************************************
012400* Now we have to have finished and can return to our invoker.   *
012500*****************************************************************
012600* Now return to CICS
012700     MOVE WS-BANK-DATA-AREAS (1:LENGTH OF WS-BANK-DATA-AREAS)
012800       TO DFHCOMMAREA (1:WS-SAVED-EIBCALEN).
012900     EXEC CICS
013000          RETURN
013100     END-EXEC.
013200     GOBACK.
013300
013400*****************************************************************
013500* Screen processing for MBANK10                                 *
013600*---------------------------------------------------------------*
013700* Retrieve data from screen and format it                       *
013800*****************************************************************
013900 SCREEN10-READ.
014000     MOVE 'BBANK10P' TO WS-BUSINESS-LOGIC-PGM.
014100     IF BANK-AID-CLEAR
014200        SET BANK-AID-PFK03 TO TRUE
014300        GO TO SCREEN10-READ-EXIT
014400     END-IF.
014500     IF BANK-LAST-MAPSET IS EQUAL TO SPACES
014600        GO TO SCREEN10-READ-EXIT
014700     END-IF.
014800     IF BANK-ENV-CICS
014900        GO TO SCREEN10-READ-CICS
015000     ELSE
015100        GO TO SCREEN10-READ-INET
015200     END-IF.
015300
015400 SCREEN10-READ-CICS.
015500     IF BANK-HELP-INACTIVE
015600        EXEC CICS RECEIVE MAP('BANK10A')
015700                          MAPSET('MBANK10')
015800        END-EXEC
015900     ELSE
016000        EXEC CICS RECEIVE MAP('HELP10A')
016100                          MAPSET('MBANK10')
016200        END-EXEC
016300        GO TO SCREEN10-READ-EXIT
016400     END-IF.
016500
016600     IF USERIDL IN BANK10AI IS EQUAL TO 0
016700           MOVE LOW-VALUES TO BANK-SIGNON-ID
016800     ELSE
016900        MOVE USERIDI IN BANK10AI
017000          TO BANK-SIGNON-ID (1:USERIDL IN BANK10AI)
017100     END-IF.
017200
017300     IF PSWDL IN BANK10AI IS EQUAL TO 0
017400        MOVE LOW-VALUES TO BANK-PSWD
017500     ELSE
017600        MOVE PSWDI IN BANK10AI
017700          TO BANK-PSWD (1:PSWDL IN BANK10AI)
017800     END-IF.
017900
018000     GO TO SCREEN10-READ-EXIT.
018100
018200 SCREEN10-READ-INET.
018300     MOVE EXT-IP10-USERID TO BANK-SIGNON-ID.
018400     MOVE EXT-IP10-PSWD TO BANK-PSWD.
018500     GO TO SCREEN10-READ-EXIT.
018600
018700 SCREEN10-READ-EXIT.
018800     EXIT.
018900
019000*****************************************************************
019100* Screen processing for SCREEN10 (BANK10/HELP10)                *
019200*---------------------------------------------------------------*
019300* Build the output screen and send it                           *
019400*****************************************************************
019500 SCREEN10-BUILD-AND-SEND.
019600* Clear map area, get date & time and move to the map
019700     MOVE LOW-VALUES TO BANK10AO.
019800     MOVE EIBTIME TO DD-TIME-INPUT-N.
019900     MOVE EIBDATE TO DDI-DATA-YYDDD-YYDDD-N.
020000     SET DDI-YYDDD TO TRUE.
020100     SET DDO-DD-MMM-YYYY TO TRUE.
020200     PERFORM CALL-DATECONV THRU
020300             CALL-DATECONV-EXIT.
020400* Ensure the last map fields are correct
020500     IF BANK-HELP-ACTIVE
020600        MOVE 'MBANK10' TO BANK-LAST-MAPSET
020700        MOVE 'HELP10A' TO BANK-LAST-MAP
020800     ELSE
020900        MOVE 'MBANK10' TO BANK-LAST-MAPSET
021000        MOVE 'BANK10A' TO BANK-LAST-MAP
021100     END-IF.
021200     IF BANK-ENV-CICS
021300        GO TO SCREEN10-BUILD-AND-SEND-CICS
021400     ELSE
021500        GO TO SCREEN10-BUILD-AND-SEND-INET
021600     END-IF.
021700
021800 SCREEN10-BUILD-AND-SEND-CICS.
021900     IF BANK-LAST-MAP IS EQUAL TO 'BANK10A'
022000        GO TO BANK10-BUILD-AND-SEND-CICS
022100     END-IF.
022200     IF BANK-LAST-MAP IS EQUAL TO 'HELP10A'
022300        GO TO HELP10-BUILD-AND-SEND-CICS
022400     END-IF.
022500     MOVE WS-PROGRAM-ID TO ABEND-CULPRIT
022600     MOVE '0003' TO ABEND-CODE
022700     MOVE SPACES TO ABEND-REASON
022800*     COPY CABENDPO.
022800     PERFORM ZZ-ABEND.
022900     GOBACK.
023000
023100 BANK10-BUILD-AND-SEND-CICS.
023200*     COPY CSCRNHP1 REPLACING ==<<SCRN>>== BY ==BANK10AO==.
001200     CALL 'SCUSTOMP' USING SCREEN-TITLES.
001300     MOVE SCREEN-TITLE1 TO HEAD1O IN BANK10AO.
001400     MOVE SCREEN-TITLE2 TO HEAD2O IN BANK10AO.
001500

023300*    COPY CVERSNP1 REPLACING ==<<SCRN>>== BY ==BANK10AO==.
001200     CALL 'SVERSONP' USING VERSION.
001300     MOVE VERSION TO VERO IN BANK10AO.
       
023400     MOVE WS-TRAN-ID TO TRANO IN BANK10AO.
023500     MOVE DD-TIME-OUTPUT TO TIMEO IN BANK10AO.
023600     MOVE DDO-DATA TO DATEO IN BANK10AO.
023700* Move in any error message
023800     MOVE BANK-ERROR-MSG TO ERRMSGO IN BANK10AO.
023900* Move in screen specific fields
024000     MOVE -1 TO USERIDL IN BANK10AI.
024100     MOVE BANK-SIGNON-ID TO USERIDO IN BANK10AO.
024200     MOVE -1 TO PSWDL IN BANK10AI.
024300     MOVE BANK-PSWD TO PSWDO IN BANK10AO.
024400* Turn colour off if required
024500     IF COLOUR-OFF
024600        MOVE DFHGREEN TO TXT01C IN BANK10AO
024700        MOVE DFHGREEN TO SCRNC IN BANK10AO
024800        MOVE DFHGREEN TO HEAD1C IN BANK10AO
024900        MOVE DFHGREEN TO DATEC IN BANK10AO
025000        MOVE DFHGREEN TO TXT02C IN BANK10AO
025100        MOVE DFHGREEN TO TRANC IN BANK10AO
025200        MOVE DFHGREEN TO HEAD2C IN BANK10AO
025300        MOVE DFHGREEN TO TIMEC IN BANK10AO
025400        MOVE DFHGREEN TO TXT03C IN BANK10AO
025500        MOVE DFHGREEN TO TXT04C IN BANK10AO
025600        MOVE DFHGREEN TO TXT05C IN BANK10AO
025700        MOVE DFHGREEN TO TXT06C IN BANK10AO
025800        MOVE DFHGREEN TO USERIDC IN BANK10AO
025900        MOVE DFHGREEN TO TXT07C IN BANK10AO
026000        MOVE DFHGREEN TO PSWDC IN BANK10AO
026100        MOVE DFHGREEN TO ERRMSGC IN BANK10AO
026200        MOVE DFHGREEN TO TXT08C IN BANK10AO
026300        MOVE DFHGREEN TO VERC IN BANK10AO
026400     END-IF.
026500
026600     EXEC CICS SEND MAP('BANK10A')
026700                    MAPSET('MBANK10')
026800                    ERASE
026900                    FREEKB
027000     END-EXEC.
027100     GO TO SCREEN10-BUILD-AND-SEND-EXIT.
027200
027300 HELP10-BUILD-AND-SEND-CICS.
027400*    COPY CSCRNHP2 REPLACING ==:OPTN:== BY ==BANK==
027500*                            ==<<SCRN>>== BY ==HELP10AO==.
001200     CALL 'SCUSTOMP' USING SCREEN-TITLES.
001300     MOVE SCREEN-TITLE1 TO AHEAD1O IN HELP10AO.
001400     MOVE SCREEN-TITLE2 TO AHEAD2O IN HELP10AO.
001500     CALL 'SVERSONP' USING VERSION.
001600     MOVE VERSION TO AVERO IN HELP10AO.
001700     MOVE WS-TRAN-ID TO ATRANO IN HELP10AO.
001800     MOVE DD-TIME-OUTPUT TO ATIMEO IN HELP10AO.
001900     MOVE DDO-DATA TO ADATEO IN HELP10AO.
002000* Move in any error message
002100* Move in screen specific fields
002200        MOVE BANK-HELP-LINE (01) TO AHLP01O IN HELP10AO.
002300        MOVE BANK-HELP-LINE (02) TO AHLP02O IN HELP10AO.
002400        MOVE BANK-HELP-LINE (03) TO AHLP03O IN HELP10AO.
002500        MOVE BANK-HELP-LINE (04) TO AHLP04O IN HELP10AO.
002600        MOVE BANK-HELP-LINE (05) TO AHLP05O IN HELP10AO.
002700        MOVE BANK-HELP-LINE (06) TO AHLP06O IN HELP10AO.
002800        MOVE BANK-HELP-LINE (07) TO AHLP07O IN HELP10AO.
002900        MOVE BANK-HELP-LINE (08) TO AHLP08O IN HELP10AO.
003000        MOVE BANK-HELP-LINE (09) TO AHLP09O IN HELP10AO.
003100        MOVE BANK-HELP-LINE (10) TO AHLP10O IN HELP10AO.
003200        MOVE BANK-HELP-LINE (11) TO AHLP11O IN HELP10AO.
003300        MOVE BANK-HELP-LINE (12) TO AHLP12O IN HELP10AO.
003400        MOVE BANK-HELP-LINE (13) TO AHLP13O IN HELP10AO.
003500        MOVE BANK-HELP-LINE (14) TO AHLP14O IN HELP10AO.
003600        MOVE BANK-HELP-LINE (15) TO AHLP15O IN HELP10AO.
003700        MOVE BANK-HELP-LINE (16) TO AHLP16O IN HELP10AO.
003800        MOVE BANK-HELP-LINE (17) TO AHLP17O IN HELP10AO.
003900        MOVE BANK-HELP-LINE (18) TO AHLP18O IN HELP10AO.
004000        MOVE BANK-HELP-LINE (19) TO AHLP19O IN HELP10AO.
004100* Turn colour off if required
004200     IF COLOUR-OFF
004300        MOVE DFHGREEN TO ATXT01C IN HELP10AO
004400        MOVE DFHGREEN TO ASCRNC IN HELP10AO
004500        MOVE DFHGREEN TO AHEAD1C IN HELP10AO
004600        MOVE DFHGREEN TO ADATEC IN HELP10AO
004700        MOVE DFHGREEN TO ATXT02C IN HELP10AO
004800        MOVE DFHGREEN TO ATRANC IN HELP10AO
004900        MOVE DFHGREEN TO AHEAD2C IN HELP10AO
005000        MOVE DFHGREEN TO ATIMEC IN HELP10AO
005100        MOVE DFHGREEN TO AHLP01C IN HELP10AO
005200        MOVE DFHGREEN TO AHLP02C IN HELP10AO
005300        MOVE DFHGREEN TO AHLP03C IN HELP10AO
005400        MOVE DFHGREEN TO AHLP04C IN HELP10AO
005500        MOVE DFHGREEN TO AHLP05C IN HELP10AO
005600        MOVE DFHGREEN TO AHLP06C IN HELP10AO
005700        MOVE DFHGREEN TO AHLP07C IN HELP10AO
005800        MOVE DFHGREEN TO AHLP08C IN HELP10AO
005900        MOVE DFHGREEN TO AHLP09C IN HELP10AO
006000        MOVE DFHGREEN TO AHLP10C IN HELP10AO
006100        MOVE DFHGREEN TO AHLP11C IN HELP10AO
006200        MOVE DFHGREEN TO AHLP12C IN HELP10AO
006300        MOVE DFHGREEN TO AHLP13C IN HELP10AO
006400        MOVE DFHGREEN TO AHLP14C IN HELP10AO
006500        MOVE DFHGREEN TO AHLP15C IN HELP10AO
006600        MOVE DFHGREEN TO AHLP16C IN HELP10AO
006700        MOVE DFHGREEN TO AHLP17C IN HELP10AO
006800        MOVE DFHGREEN TO AHLP18C IN HELP10AO
006900        MOVE DFHGREEN TO AHLP19C IN HELP10AO
007000        MOVE DFHGREEN TO ATXT03C IN HELP10AO
007100        MOVE DFHGREEN TO AVERC IN HELP10AO
007200     END-IF.
027600
027700     EXEC CICS SEND MAP('HELP10A')
027800                    MAPSET('MBANK10')
027900                    ERASE
028000                    FREEKB
028100     END-EXEC.
028200     GO TO SCREEN10-BUILD-AND-SEND-EXIT.
028300
028400
028500 SCREEN10-BUILD-AND-SEND-INET.
028600     MOVE SPACES TO EXT-OP-DATA.
028700     MOVE WS-TRAN-ID TO EXT-OP-TRAN.
028800     MOVE DDO-DATA TO EXT-OP-DATE.
028900     MOVE DD-TIME-OUTPUT TO EXT-OP-TIME.
029000     CALL 'SCUSTOMP' USING SCREEN-TITLES.
029100     MOVE SCREEN-TITLE1 TO EXT-OP-HEAD1.
029200     MOVE SCREEN-TITLE2 TO EXT-OP-HEAD2.
029300     CALL 'SVERSONP' USING SCREEN-TITLES.
029400     MOVE VERSION TO EXT-OP-VERSION.
029500* Move in screen name
029600     MOVE 'BANK10' TO EXT-OP-SCREEN.
029700* Move in userid and any error message
029800     MOVE BANK-ERROR-MSG TO EXT-OP-ERR-MSG.
029900     MOVE BANK-SIGNON-ID TO EXT-OP-USERID.
030000     MOVE BANK-USERID-NAME TO EXT-OP-NAME.
030100* Move in screen specific fields
030200     MOVE BANK-PSWD TO EXT-OP10-PSWD.
030300     GO TO SCREEN10-BUILD-AND-SEND-EXIT.
030400
030500 SCREEN10-BUILD-AND-SEND-EXIT.
030600     EXIT.
030700
030800*****************************************************************
030900* Call common routine to perform date conversions               *
031000*****************************************************************
031100 CALL-DATECONV.
031200     MOVE BANK-ENV TO DD-ENV.
031300     MOVE 'UDATECNV' TO WS-DYNAMIC-PGM.
031400     CALL WS-DYNAMIC-PGM USING WS-TIME-DATE-WORK-AREA.
031500 CALL-DATECONV-EXIT.
031600     EXIT.
031700
       ZZ-ABEND SECTION.
001600    
           STRING ABEND-CULPRIT DELIMITED BY SIZE
001700            ' Abend ' DELIMITED BY SIZE
001800            ABEND-CODE DELIMITED BY SIZE
001900            ' - ' DELIMITED BY SIZE
002000             ABEND-REASON DELIMITED BY SIZE
002100         INTO ABEND-MSG
           END-STRING.
002200     
           EXEC CICS WRITE
002300               OPERATOR
002400               TEXT(ABEND-MSG)
002500               TEXTLENGTH(LENGTH OF ABEND-MSG)
002600     END-EXEC.
002700         
           EXEC CICS ABEND
002800           ABCODE(ABEND-CODE)
002900     END-EXEC.

031800* $ Version 5.99c sequenced on Wednesday 3 Mar 2011 at 1:00pm
